rl -s learning on
indifferent-selection -g
indifferent-selection -e 0.2 #20%deviationfromgreedy

sp {propose*initialize-escape
    (state <s> ^superstate nil 
              -^name)
-->
    (<s> ^operator <o> +)
    (<o> ^name initialize-escape)
}

sp {apply*initialize-escape
    (state <s> ^operator <op>)
    (<op> ^name initialize-escape)
-->
    (<s> ^name escape
         ^height 150
         ^location start
         ^object <el> <tw> <st> <tr1> <tr2> <wn>
         ^direction <d1> <d2> <d3> <d4> <d5> <d6>
         ^combination <sling> <stonetwig> <stonelastic>
         ^shoot-dir <up> <down> <right> <left> <center>
         ^escaped no
         ^last_action <azione>)
   
    (<azione> ^name nil)   
    
    (<d1> ^name window_loc ^item <wn> ^reward 0)
    (<d2> ^name elastic_loc ^item <el> ^reward -1)
    (<d3> ^name stones_loc ^item <st> ^reward -1)
    (<d4> ^name twig_loc ^item <tw> ^reward -1)
    (<d5> ^name trunk1_loc ^item <tr1> ^reward -1)
    (<d6> ^name trunk2_loc ^item <tr2> ^reward -1)

    (<sling> ^name sling ^item1 <el> ^item2 <tw> ^crafted no ^reward +1)
    (<stonetwig> ^name stonetwig ^item1 <st> ^item2 <tw> ^crafted no ^reward -1)
    (<stonelastic> ^name stonelastic ^item1 <st> ^item2 <el> ^crafted no ^reward -1)

    (<up> ^name up ^shooted no  ^reward -1)
    (<down> ^name down ^shooted no ^reward -1)
    (<right> ^name right ^shooted no ^reward +1)
    (<left> ^name left ^shooted no ^reward -1)
    (<center> ^name center ^shooted no ^reward -1)

    (<el> ^name elastic ^taken no ^used no) 
    (<tw> ^name twig ^taken no ^used no)
    (<st> ^name stones ^taken no ^used no) 
    (<tr1> ^name trunk_1 ^taken no ^height 100 ^used no)   
    (<tr2> ^name trunk_2 ^taken no ^height 100 ^used no)
    (<wn> ^name window ^height 350 ^broken no)
    
}

sp {htcompare*move*take 
   (state <s>  ^name escape) 
   (<s> ^operator <op-move> +) 
   (<s> ^operator <op-take> +) 
   (<op-move> ^name move)
   (<op-take> ^name take) 
-->
   (<s> ^operator <op-take> > <op-move>)
}

sp {htcompare*move*craft 
   (state <s>  ^name escape) 
   (<s> ^operator <op-move> +) 
   (<s> ^operator <op-craft> +) 
   (<op-move> ^name move)
   (<op-craft> ^name craft) 
-->
   (<s> ^operator <op-craft> = <op-move>)
}

sp {htcompare*take*craft 
   (state <s>  ^name escape) 
   (<s> ^operator <op-take> +) 
   (<s> ^operator <op-craft> +) 
   (<op-take> ^name take)
   (<op-craft> ^name craft) 
-->
   (<s> ^operator <op-take> > <op-craft>)
}

sp {htcompare*shoot*move 
   (state <s>  ^name escape) 
   (<s> ^operator <op-move> +) 
   (<s> ^operator <op-shoot> +) 
   (<op-move> ^name move)
   (<op-shoot> ^name shoot) 
-->
   (<s> ^operator <op-shoot> > <op-move>)
}

sp {bestmove*escape 
   (state <s>  ^name escape) 
   (<s> ^operator <op-escape> +) 
   (<op-escape> ^name escape) 
-->
   (<s> ^operator <op-escape> >)
}

sp {htcompare*stack*move 
   (state <s>  ^name escape) 
   (<s> ^operator <op-move> +) 
   (<s> ^operator <op-stack> +) 
   (<op-move> ^name move)
   (<op-stack> ^name stack) 
-->
   (<s> ^operator <op-stack> > <op-move>)
}

sp {htcompare*shoot*stack 
   (state <s>  ^name escape) 
   (<s> ^operator <op-stack> +) 
   (<s> ^operator <op-shoot> +) 
   (<op-stack> ^name stack)
   (<op-shoot> ^name shoot) 
-->
   (<s> ^operator <op-shoot> = <op-stack>)
}

sp {htcompare*craft*stack 
   (state <s>  ^name escape) 
   (<s> ^operator <op-stack> +) 
   (<s> ^operator <op-craft> +) 
   (<op-stack> ^name stack)
   (<op-craft> ^name craft) 
-->
   (<s> ^operator <op-craft> = <op-stack>)
}

### Propose & Apply ### 
sp {propose*move
   (state <s> ^name escape
              ^direction <d> 
              ^location <from>)
   (<d> ^name <dir>)
   -(<d> ^name <from>)
-->
   (<s> ^operator <op> +) 
   (<op> ^name move 
         ^dir <dir>)
}

sp {apply*move
   (state <s> ^operator <op> 
              ^location <loc>
              ^last_action <la>)
   (<op> ^name move 
         ^dir <dir>)
-->
   (<s> ^location <loc> - <dir>)
   (<la> ^name <name> - move +
         ^dir <dir>)
}

sp {propose*craft
   (state <s> ^name escape
              ^combination <c>)
   (<c> ^item1 <item1> ^item2 <item2> ^crafted no)
   (<item1> ^taken yes ^used no)
   (<item2> ^taken yes ^used no)
-->
   (<s> ^operator <op> +) 
   (<op> ^name craft 
         ^combination <c>)
}

sp {apply*craft
   (state <s> ^name escape
              ^operator <op>
              ^last_action <la>)
   (<op> ^name craft 
         ^combination <c>)
   (<c> ^name <c-name> ^item1 <item1> ^item2 <item2>)        
-->
   (<c> ^crafted no - yes +)
   (<item1> ^used no - yes +)
   (<item2> ^used no - yes +)
   (<la> ^name <name> - craft +
         ^item <c>)
   (write (crlf) |Crafted item:| <c-name>)
}

sp {propose*take
   (state <s> ^name escape
              ^location <loc>
              ^direction <dir>)
   
   (<dir> ^name <loc>
          ^item <item>)
   
   (<item> ^name <item_name> ^taken no)
   -(<item> ^name window)
-->
   (<s> ^operator <op> +)
   (<op> ^name take
         ^item <item>
         ^item_name <item_name>)
}

sp {apply*take
   (state <s> ^name escape
              ^operator <op>
              ^object <obj>)
   (<op> ^name take 
         ^item <item>
         ^item_name <item_name>)  
   (<obj> ^name <item_name>)        
-->
   (<obj> ^taken no - yes +)
   #(write (crlf) |Taken item:| <item_name>)
}

sp {propose*shoot
   (state <s> ^name escape
              ^location window_loc
              ^combination <c>
              ^object <st> <wn>
              ^shoot-dir <sd>)
   (<st> ^name stones ^taken yes)
   (<c> ^name sling ^crafted yes)
   (<wn> ^name window ^broken no)
   (<sd> ^shooted no)
-->
   (<s> ^operator <op> +)
   (<op> ^name shoot
         ^shoot-dir <sd>)
}

sp {apply*shoot
   (state <s> ^name escape
              ^operator <op>
              ^object <wn>
              ^last_action <la>)
   (<op> ^name shoot
         ^shoot-dir <sd>)
   (<wn> ^name window)
   (<sd> ^name <sd-name>)
-->
   (<wn> ^broken no -)
   (<wn> ^broken (ifeq <sd-name> right yes no) +)
   (<sd> ^shooted no - yes +)
   (<la> ^name <name> - shoot +
         ^shoot-dir <sd>)
   #(write (crlf) |Shooted to| <sd-name>)
   (<s> ^operator <op> -)
}

sp {propose*stack
   (state <s> ^name escape
              ^location window_loc
              ^object <ob1> <ob2>)
   (<ob1> ^name trunk_1 ^taken yes ^used no)
   (<ob2> ^name trunk_2 ^taken yes ^used no)
-->
   (<s> ^operator <op> +)
   (<op> ^name stack
         ^item_1 <ob1>
         ^item_2 <ob2>)
}

sp {apply*stack
   (state <s> ^name escape
              ^height <h>
              ^operator <op>)
   (<op> ^name stack 
         ^item_1 <ob1>
         ^item_2 <ob2>) 
   (<ob1> ^height <h1>)
   (<ob2> ^height <h2>)       
-->
   (<s> ^height <h> - (+ <h> <h1> <h2>) +)
   (<ob1> ^used no - yes +)
   (<ob2> ^used no - yes +)
}

sp {propose*escape
   (state <s> ^name escape
              ^location window_loc
              ^object <wn>
              ^height 350)
   (<wn> ^name window ^broken yes)
-->
   (<s> ^operator <op> +)
   (<op> ^name escape)
}

sp {apply*escape
   (state <s> ^name escape
              ^operator <op>)
   (<op> ^name escape)       
-->
   (<s> ^escaped no - yes +)
   (write(crlf) | Escaped! |)
}

### RL Rules
sp {rl*sling
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name craft 
         ^combination <sling>) 
   (<sling> ^name sling)
-->
   (<s> ^operator <op> = 0)
}

sp {rl*stonelastic
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name craft 
         ^combination <stonelastic>) 
   (<stonelastic> ^name stonelastic)
-->
   (<s> ^operator <op> = 0)
}

sp {rl*stonetwig
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name craft 
         ^combination <stonetwig>) 
   (<stonetwig> ^name stonetwig)
-->
   (<s> ^operator <op> = 0)
}

sp {rl*window
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name move 
         ^dir window_loc) 
-->
   (<s> ^operator <op> = 0)
}

sp {rl*elastic
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name move 
         ^dir elastic_loc) 
-->
   (<s> ^operator <op> = 0)
}

sp {rl*stones
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name move 
         ^dir stones_loc) 
-->
   (<s> ^operator <op> = 0)
}
sp {rl*twig
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name move 
         ^dir twig_loc) 
-->
   (<s> ^operator <op> = 0)
}
sp {rl*trunk1
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name move 
         ^dir trunk1_loc) 
-->
   (<s> ^operator <op> = 0)
}
sp {rl*trunk2
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name move 
         ^dir trunk2_loc) 
-->
   (<s> ^operator <op> = 0)
}

sp {rl*shoot-up
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name shoot 
         ^shoot-dir (<dir> ^name up))
 
-->
   (<s> ^operator <op> = 0)
}

sp {rl*shoot-down
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name shoot 
         ^shoot-dir (<dir> ^name down)) 
-->
   (<s> ^operator <op> = 0)
}

sp {rl*shoot-right
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name shoot 
         ^shoot-dir (<dir> ^name right)) 
-->
   (<s> ^operator <op> = 0)
}

sp {rl*shoot-left
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name shoot 
         ^shoot-dir (<dir> ^name left)) 
-->
   (<s> ^operator <op> = 0)
}

sp {rl*shoot-center
   (state <s> ^name escape 
              ^operator <op> +) 
   (<op> ^name shoot 
         ^shoot-dir (<dir> ^name center)) 
-->
   (<s> ^operator <op> = 0)
}

sp {elaborate*reward-movement
   (state <s> ^name escape
              ^reward-link <r> 
              ^last_action <la>) 
   (<la> ^name move ^dir <dir>)
   (<dir> ^reward <d-reward>) 
-->
   (<r> ^reward <rr>) 
   (<rr> ^value <d-reward>)
}

sp {elaborate*reward-crafting
   (state <s> ^name escape
              ^reward-link <r> 
              ^last_action <la>)
   (<la> ^name craft ^item <item>)
   (<item> ^crafted yes ^reward <d-reward>) 
-->
   (<r> ^reward <rr>) 
   (<rr> ^value <d-reward>)
}

sp {elaborate*reward-shooting
   (state <s> ^name escape
              ^reward-link <r> 
              ^last_action <la>) 
   (<la> ^name shoot ^shoot-dir <sd>)
   (<sd> ^reward <d-reward>) 
-->
   (<r> ^reward <rr>) 
   (<rr> ^value <d-reward>)
}



sp {elaborate*done-succes
   (state <s> ^name escape
              ^escaped yes) 
-->
   (write (crlf) |Sei scappato!|)
   (halt)
}

sp {elaborate*done-failure
   (state <s> ^name escape
              ^combination <c1> <c2>)
   (<c1> ^name sling ^crafted no)
   (<c2> ^name <name> ^crafted yes)

-->
   (write (crlf) |Hai perso!|)
   (halt)
}  